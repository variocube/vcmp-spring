plugins {
    id 'java-library'
    id 'org.springframework.boot' version '2.4.5' apply false
    id 'io.spring.dependency-management' version '1.1.4'
    id "io.freefair.lombok" version "5.2.1"
    id 'maven-publish'
}

group 'com.variocube'

repositories {
    mavenCentral()
}

jar {
    enabled = true
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    api group: 'org.springframework.boot', name: 'spring-boot-starter', version: "[2.0, 3.0)"
    api group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "[2.0, 3.0)"
    api group: 'org.springframework.boot', name: 'spring-boot-starter-websocket', version: "[2.0, 3.0)"

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.0.3'
}

lombok.config['lombok.anyConstructor.addConstructorProperties'] = "true"

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url "s3://og-repository/maven"
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}