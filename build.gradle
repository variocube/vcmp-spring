plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.1.5' apply false
    id 'io.spring.dependency-management' version '1.1.4'
    id "io.freefair.lombok" version "8.1.0"
    id 'maven-publish'
}

group 'com.variocube'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

jar {
    enabled = true
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter:[3.0, 4.0)'
    api 'org.springframework.data:spring-data-commons:[3.0, 4.0)'
    api 'org.springframework.boot:spring-boot-starter-web:[3.0, 4.0)'
    api 'org.springframework.boot:spring-boot-starter-websocket:[3.0, 4.0)'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.2.0'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url "s3://og-repository/maven"
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}